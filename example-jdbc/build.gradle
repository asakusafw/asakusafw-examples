buildscript {
    repositories {
        maven { url 'http://asakusafw.s3.amazonaws.com/maven/releases' }
    }
    dependencies {
        classpath group: 'com.asakusafw', name: 'asakusa-gradle-plugins', version: '0.7-SNAPSHOT'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion '1.12'
    jarFile file('.buildtools/gradlew.jar')
}

apply plugin: 'asakusafw'
apply plugin: 'asakusafw-organizer'
apply plugin: 'eclipse'

asakusafw {
    asakusafwVersion projectAsakusafwVersion

    modelgen {
        modelgenSourcePackage 'com.asakusafw.example.jdbc.modelgen'
    }
    compiler {
        compiledSourcePackage 'com.asakusafw.example.jdbc.batchapp'
    }
}

asakusafwOrganizer {
    asakusafwVersion asakusafw.asakusafwVersion
}

dependencies {
    compile group: 'com.asakusafw.sdk', name: 'asakusa-sdk-core', version: asakusafw.asakusafwVersion
    compile group: 'com.asakusafw.sdk', name: 'asakusa-sdk-directio', version: asakusafw.asakusafwVersion
    compile group: 'com.asakusafw.sdk', name: 'asakusa-sdk-windgate', version: asakusafw.asakusafwVersion

    provided (group: 'org.apache.hadoop', name: 'hadoop-client', version: projectHadoopVersion) {
        exclude module: 'junit'
        exclude module: 'mockito-all'
        exclude module: 'slf4j-log4j12'
    }
}

configurations {
    jdbcDriver
}

dependencies {
    jdbcDriver group: 'postgresql', name: 'postgresql', version: '9.1-901-1.jdbc4'
}

afterEvaluate {
    installAsakusafw {
        doLast {
            ant.replace(
                file: "${System.env.ASAKUSA_HOME}/windgate/profile/asakusa.properties",
                token: '#resource.jdbc',
                value: 'resource.jdbc')
            copy {
                from configurations.jdbcDriver
                into "${System.env.ASAKUSA_HOME}/windgate/plugin"
            }
        }
    }
}

task setupDDL << {
    def profileWindGate = new Properties()
    file("${System.env.ASAKUSA_HOME}/windgate/profile/asakusa.properties").withInputStream {
        stream -> profileWindGate.load(stream)
    }

    ant.sql(
        driver: profileWindGate['resource.jdbc.driver'],
        url: profileWindGate['resource.jdbc.url'],
        userid: profileWindGate['resource.jdbc.user'],
        password: profileWindGate['resource.jdbc.password'],
        encoding: 'UTF-8',
        classpath: configurations.jdbcDriver.asPath) {
        fileset(dir: 'src/main/sql')
    }
}
